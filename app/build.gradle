plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
}

// This file is used to factorize all configurations of the project
apply from: "${rootDir}/configuration.gradle"

// This file is used to factorize all dependencies of the project
apply from: "$rootDir/dependencies.gradle"

// This file is used to separate flavors declarations
apply from: "$rootDir/flavors.gradle"

android {
    namespace config.applicationId

    compileSdk config.currentSdk
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        applicationId config.applicationId
        minSdk config.minSdkVersion
        targetSdk config.currentSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner config.testInstrumentationRunner
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility config.javaVersion
        targetCompatibility config.javaVersion
    }

    kotlinOptions {
        jvmTarget = config.jvmTarget
    }

    buildFeatures {
        dataBinding = true
    }
}

dependencies {

    // Jetpack
    implementation(jetpack.appcompat)
    implementation(jetpack.recyclerview)

    // Livedata
    implementation(lifecycle.extensions)
    implementation(lifecycle.viewmodel)
    implementation(lifecycle.livedata)

    // UI
    implementation(ui.material)

    // Navigation
    implementation(navigation.fragment)
    implementation(navigation.ui)
    
    implementation project(path: ':framework')
}
